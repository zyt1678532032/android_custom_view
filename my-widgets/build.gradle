plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace 'com.zyt.widgets'
    compileSdk 33

    defaultConfig {
        minSdk 24

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    viewBinding {
        enable = true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

// 发布到github的package
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/zyt1678532032/android_custom_view")

            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                println "username: $username; password: $password"
            }
        }
    }
    publications {
        gpr(MavenPublication) {

            groupId = 'com.github.zyt'
            artifactId = 'test'
            version = '0.0.3'

            artifact("$buildDir/outputs/aar/$project.name-release.aar")
        }
    }
}

// 上传脚本中新增task，类型为Jar
tasks.register('androidSourceJar', Jar) {
    // from project.android.sourceSets.main.java.sourceFiles // 不包括Kotlin文件
    from project.android.sourceSets.main.java.getSrcDirs() // 包括Kotlin文件
    archiveClassifier = 'sources'
}

// 发布到本地MavenLocal
publishing {
    // 定义发布到哪
    // publishPluginPublicationToRepoRepository 发布到 plugin/build/repo
    // publishPluginPublicationToMavenLocal 发布到 ~/.m2/repository(本地的其他项目点击这个就可以)
    repositories {
        maven {
            name = 'repo'
            url = layout.buildDirectory.dir("repo")
        }
    }
    // 定义发布内容
    publications {
        zytwidgets(MavenPublication) {
            groupId = 'com.zyt.widgets'
            artifactId = 'my-widgets'
            version = '0.0.2-SNAPSHOT'
            // 从aar中发布, gradle插件点击build就可以生成aar
            artifact("$buildDir/outputs/aar/$project.name-debug.aar")
            artifact androidSourceJar // 上传源码
        }
    }
}
